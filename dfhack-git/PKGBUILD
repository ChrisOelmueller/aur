# Maintainer: Chris Oelmueller <chris.oelmueller@gmail.com>
# Contributor: Gazj < garyjames82 AT gmail DOT com>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>

# Right now, DF2014 on Arch is copying lots of stuff to ~/.dwarffortress/
# from /opt/dwarffortress/ and runs DF in that directory.
# Thus, DFHack requires the ~ path supplied when asking about install prefix
# (as opposed to the one in /opt) which is incredibly clumsy and the reason
# this package is not submitted anywhere AUR-official.
#
# BIG FAT Huge Important Note
# = = = = = = = = = = = = = =
# Replace the dummy user in build() before building this package!
# To minimize potential issues, it's probably best to run the (not hacked)
# `dwarffortress` binary once before installing as well.

pkgname=dfhack-git
pkgver=0.40.11.r1
pkgrel=1
pkgdesc="A cross-platform library for accessing Dwarf Fortress memory"
arch=('i686' 'x86_64')
url="https://github.com/DFHack/dfhack"
license=('custom:zlib')
depends=(gcc-libs libx11 allegro perl-xml-libxml)
makedepends=(cmake git perl-xml-libxslt)
if [[ $CARCH == 'x86_64' ]]; then
  depends+=(lib32-allegro)
  makedepends+=(gcc-multilib perl-xml-libxml)
fi
conflicts=(dfhack dfhack-myne)
provides=(dfhack)
replaces=(stonesense-svn)
# Submodules are tweaked in prepare()
source=(
  "git://github.com/DFHack/dfhack.git#branch=develop"
  "git://github.com/DFHack/stonesense.git"
  "git://github.com/DFHack/isoworld.git"
  "git://github.com/DFHack/DF2MC.git"
  "git://github.com/DFHack/df-structures.git"
  "git://github.com/DFHack/clsocket.git"
  "dwarffortress-hacked"
  "allegro_color.diff"
)
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
         '3862ac0ca51e4e35e8ab48b24f733d03'
         '14b68204f5426a075f5e661bdf4778c2')
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
            'b5975c44d76ecf8dbde4c08325788985c3653bf33c44f1e7ab00b744b3c4d3eb'
            'c1a0540111cd5c07cd00df4bbe2cef97108b1d39d4b50c3bd210c2014d4edc55')

pkgver() {
  cd "$srcdir/${pkgname%-*}"
  git describe --long --tags | sed -r 's/([^-]*-g)/\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-*}"
  git submodule init
  git config submodule.plugins/stonesense.url $srcdir/stonesense
  git config submodule.plugins/isoworld.url $srcdir/isoworld
  git config submodule.plugins/df2mc.url $srcdir/DF2MC
  git config submodule.library/xml.url $srcdir/df-structures
  git config submodule.depends/clsocket.url $srcdir/clsocket
  git submodule update
  cd "plugins/stonesense/"
  patch -p1 -i "$srcdir/allegro_color.diff"
}

build() {
  cd "$srcdir/${pkgname%-*}"
  cd build
  # Make sure to replace this with the correct user on your machine!
  _DWARFUSER="$USER"
  cmake .. \
    -DCMAKE_BUILD_TYPE:string=Release \
    -DCMAKE_INSTALL_PREFIX=$pkgdir/home/$_DWARFUSER/.dwarffortress \
    -DBUILD_STONESENSE=on \
    -DBUILD_LIBRARY=on \
    -DBUILD_PLUGINS=on \
    -DSTONESENSE_INTERNAL_SO=off \

  # -DCMAKE_BUILD_TYPE:string=Debug \
  # -DCMAKE_INSTALL_PREFIX=$pkgdir/opt/dwarffortress \
  # -DMEMXML_DATA_PATH:path=/usr/share/dfhack

  make
}

package() {
  cd "$srcdir/${pkgname%-*}"
  cd build
  make install

  cd ..
  install -Dm755 "$srcdir/dwarffortress-hacked" \
                 "$pkgdir/usr/bin/dwarffortress-hacked"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/dfhack/LICENSE"
}
