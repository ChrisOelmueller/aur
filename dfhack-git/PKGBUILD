# Maintainer: Chris Oelmueller <chris.oelmueller@gmail.com>
# Contributor: Elliott Seyler <emhs08@gmail.com>
# Contributor: Gazj < garyjames82 AT gmail DOT com>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
# Git version of https://aur.archlinux.org/packages/dfhack (thanks emhs!)
#
# If the package does not compile for your more recent version of
# Dwarf Fortress, try replacing the first `source` array entry with:
#   "git://github.com/DFHack/dfhack.git#branch=develop"
#
# To disable `stonesense` in case of issues with compiling it, switch
# its build flag from "on" to "off" in build():
#   -DBUILD_STONESENSE=off \

pkgname=dfhack-git
pkgver=0.42.05.alpha1.0.gcfa66c0
pkgrel=1
pkgdesc="A cross-platform library for accessing Dwarf Fortress memory"
arch=('i686' 'x86_64')
url="https://github.com/DFHack/dfhack"
license=('custom:zlib')
depends=(gcc-libs libx11 allegro4 perl-xml-libxml)
#depends=(gcc49 libx11 allegro4 perl-xml-libxml)
makedepends=(cmake git perl-xml-libxslt)
if [[ $CARCH == 'x86_64' ]]; then
  depends+=(lib32-allegro4)
  makedepends+=(gcc-libs-multilib perl-xml-libxml)
#  makedepends+=(gcc49-multilib gcc49-libs-multilib perl-xml-libxml)
fi
conflicts=(dfhack dfhack-myne)
provides=(dfhack)
replaces=(stonesense-svn)
# "git://github.com/DFHack/dfhack.git#branch=develop"
source=(
  "git://github.com/DFHack/dfhack.git#tag=0.42.05-alpha1"
  "dwarffortress-hacked"
  "dfhack-run"
)
md5sums=('SKIP'
         'cbdaa6d53b1bc3a08feb8f270316d20a'
         '27faf557adbcaec4c070f8094e206924')
sha256sums=('SKIP'
            '8d68139fb2cbb49a25999462974bff5c40ba23028c823fb666507fbc151ac52d'
            'f11870300ccd8908733c8869f93ae48b22130ee338412ffcae6f8228875db747')

pkgver() {
  cd "$srcdir/${pkgname%-*}"
  git describe --long --tags | sed -r 's/([^-]*-g)/\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-*}/"
  if [ -d "$srcdir/${pkgname%-*}/depends/clsocket" ]; then
    cd "$srcdir/${pkgname%-*}/depends/clsocket"; git stash
  fi
  if [ -d "$srcdir/${pkgname%-*}/library/xml" ]; then
    cd "$srcdir/${pkgname%-*}/library/xml"; git stash
  fi
  cd "$srcdir/${pkgname%-*}"
  git submodule update --init
  sed -ie 's/${GCC_VERSION_OUT} VERSION_GREATER "4.9.9"/${GCC_VERSION_OUT} VERSION_GREATER "9.9.9"/' CMakeLists.txt
}

build() {
  cd "$srcdir/${pkgname%-*}/build"
  cmake .. \
    -DCMAKE_BUILD_TYPE:string=Release \
    -DCMAKE_INSTALL_PREFIX=$pkgdir/opt/dwarffortress \
    -DBUILD_STONESENSE=off \
    -DBUILD_LIBRARY=on \
    -DBUILD_PLUGINS=on \
    -DSTONESENSE_INTERNAL_SO=off \

  make
}

package() {
  cd "$srcdir/${pkgname%-*}/build"
  make install

  cd ..
  install -Dm755 "$srcdir/dwarffortress-hacked" \
                 "$pkgdir/usr/bin/dwarffortress-hacked"
  install -Dm755 "$srcdir/dfhack-run" \
                 "$pkgdir/usr/bin/dfhack-run"
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/dfhack/LICENSE"
}
